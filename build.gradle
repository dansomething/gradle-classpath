plugins {
  id 'java'
  id 'application'
  id 'checkstyle'
  id 'com.diffplug.spotless' version '6.13.0'
  id 'com.github.ben-manes.versions' version '0.47.0'
  id 'com.github.gmazzo.buildconfig' version '4.0.4'
  id 'net.researchgate.release' version '3.0.2'
}

ext {
  checkstyleToolVersion = '9.3'
  junitVersion = '5.9.0'
  picocliVersion = '4.7.4'
  slf4jVersion = '1.7.36'
  toolingApiVersion = '8.1.1'
}

repositories {
  maven {
    url 'https://repo.gradle.org/gradle/libs-releases'
  }
  mavenCentral()
}

compileJava {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
  implementation "org.gradle:gradle-tooling-api:${toolingApiVersion}"
  implementation "info.picocli:picocli:${picocliVersion}"

  annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"

  runtimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"

  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

application {
  mainClass = 'com.dansomething.gradle.classpath.App'
}

test {
  useJUnitPlatform()
}

// https://github.com/gmazzo/gradle-buildconfig-plugin
buildConfig {
  packageName('com.dansomething.gradle.classpath')
  buildConfigField('String', "APP_VERSION", "\"${project.version}\"")
}

// https://docs.gradle.org/current/userguide/checkstyle_plugin.html
checkstyle {
  configFile file("${rootDir}/checkstyle.xml")
  maxWarnings 0
  toolVersion checkstyleToolVersion
}
// Be sure to ingore generated sources
checkstyleMain.source = 'src/main/java'

// https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
  format 'misc', {
    target '*.gradle', '*.md', '.gitignore', '*.xml'

    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
  java {
    targetExclude 'build/generated/**/*.*'
    googleJavaFormat()
  }
}

distTar {
  compression = Compression.GZIP
  archiveExtension = 'tar.gz'
}
